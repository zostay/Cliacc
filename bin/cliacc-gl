#!/usr/bin/env perl
use v5.14;

use FindBin;
use lib "$FindBin::Bin/../lib";
use Cliacc::DB;

my $db = Cliacc::DB->new( root => "$FindBin::Bin/.." );

my $lines = $db->connector->run(fixup => sub {
    $_->selectall_arrayref(q[
        SELECT line, reference_number, description, name, pennies
          FROM entries
          JOIN accounts ON entries.account = accounts.id
          JOIN lines ON entries.ledger = lines.id
    ], { Slice => {} });
});

for my $line (@$lines) {
    my ($left, $right) = ('', '');
    if ($line->{pennies} < 0) {
        $left = sprintf "%8.2f", $line->{pennies} * -0.01;
    }
    else {
        $right = sprintf "%8.2f", $line->{pennies} * 0.01;
    }

    printf "%-03d %-3s %-20s %-10s %8s %8s\n",
        $line->{line}, $line->{reference_number} // '-',
        $line->{description}, $line->{name}, 
        $left, $right;
} 
