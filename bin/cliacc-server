#!/usr/bin/env perl
use v5.14;

use FindBin;
use lib "$FindBin::Bin/../lib";
use Cliacc::Server;

use JSON;
use Readonly;
use Try::Tiny;
use ZMQ::LibZMQ3;
use ZMQ::Constants qw( ZMQ_REP );

Readonly my $MAX_MSG_SIZE => 8192;

my $server = Cliacc::Server->new( root_dir => "$FindBin::Bin/.." );

my $zctx = zmq_init();
my $zsock = zmq_socket($zctx, ZMQ_REP);
zmq_bind($zsock, 'tcp://127.0.0.1:5678');

while (1) {
    my ($zmessage);

    say "Waiting.";

    my $zsize = zmq_recv($zsock, $zmessage, $MAX_MSG_SIZE);

    my $json = substr($zmessage, 0, $zsize);

    my $request = decode_json($json);

    my $response;
    try {
        my $service = $server->acquire('controller', "$request->{service}");

        my $method = "$request->{action}";
        my $params = $request->{parameters};

        say "Calling $request->{service}.$method";

        my $result = $service->$method($params);

        $response = {
            success => 1,
            message => 'Success.',
            result  => $result,
        };
    }
    catch {
        $response = {
            success => 0,
            message => "$_",
        };
    }
    finally {
        $response //= {
            success => 0,
            message => 'Unknown error',
        };

        say $response->{message};

        zmq_send($zsock, encode_json($response));
    };
}


